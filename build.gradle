import org.apache.tools.ant.filters.ReplaceTokens
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'eclipse'
    id 'com.github.johnrengelman.shadow' version '5.0.0'    
}

allprojects {

	version = pluginVersion

	sourceCompatibility = 1.11
	targetCompatibility = 1.11

	repositories {
		jcenter()
  	  	maven{ url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
   	 	maven{ url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    
	processResources {
    	from(sourceSets.main.resources.srcDirs) {
        	filter ReplaceTokens, tokens: [version: version]
		}
    }
}

dependencies {
	//Spigot API
	compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
	
	compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8.1'
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8.1'
}

shadowJar {
	include 'dist.zip'
}

task testJar(type: ShadowJar) {
	dependsOn('copyExternalDependencies')

	description 'Build a test Jar'
	archiveClassifier = 'DEV'
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/server/plugins")
}

task releaseJar(type: ShadowJar) {
	dependsOn('copyExternalDependencies')

	description 'Build a release Jar'
	archiveClassifier = 'RELEASE'
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/releases")
}

//This task is for GitHub Actions only
task ghActions(type: ShadowJar) {	
	dependsOn('copyExternalDependencies')

	description 'GitHub Actions Task. Do not use in regular development!'
	version = ''
	archiveClassifier = ''
	archiveBaseName = 'output'
	
	from sourceSets.main.output
	destinationDirectory = file("$rootDir/actions")
}

task copyExternalDependencies() {
	dependsOn('copyWindowsLibrary')
	dependsOn('copyLinuxLibrary')
	dependsOn('copyWebFiles')
}

task copyWebFiles(type: Copy) {
	from "$rootDir/web/dist.zip"
	into "$buildDir/resources/main/"
}

task copyLinuxLibrary(type: Copy) {
	from "$rootDir/librconsole/target/x86_64-unknown-linux-gnu/release/librconsole.so"
	into "$buildDir/resources/main/x86_64/linux/"
}

task copyWindowsLibrary(type: Copy) {
	from "$rootDir/librconsole/target/x86_64-pc-windows-gnu/release/librconsole.dll"
	into "$buildDir/resources/main/x86_64/windows/"
}

